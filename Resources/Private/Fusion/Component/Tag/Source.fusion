prototype(Webandco.Fusion:Component.Tag.Source) < prototype(Neos.Fusion:Component) {

    cssClass = ''
    media = ''
    imageSrc = 'must-be-set'
    imageWidth = null
    imageHeight = null

    @styleguide {
        title = 'Tag: source'
        props {
            media = '(max-width: 479px)'
        }
    }

    renderer = Neos.Fusion:Tag {
        tagName = 'source'

        // assign default class of component
        attributes.class = ${props.cssClass}
        attributes.class.@if.isset = ${!String.isBlank(props.cssClass)}

        // assign media query
        attributes.media = ${props.media}
        attributes.media.@if.isset = ${!String.isBlank(props.media)}

        attributes.srcset = Webandco.Fusion:Component.Tag.Img.Src {
            imageSrc = ${props.imageSrc}
            imageWidth = ${props.imageWidth}
            imageHeight = ${props.imageHeight}
        }
    }
}
