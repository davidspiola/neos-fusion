prototype(Webandco.Fusion:Component.Tag.A) < prototype(Neos.Fusion:Component) {

    tagName = 'a'
    cssClass = 'link'
    cssModifier = Webandco.Fusion:ClassModifier
    linkTarget = false

    linkText = 'must-be-set'
    linkHref = '#'
    linkEnabled = true

    @styleguide {
        title = 'Tag: link'
        props {
            linkText = 'link text'
            linkHref = '#'
        }
    }

    renderer = Neos.Fusion:Tag {
        tagName = ${props.tagName}

        // assign default class of component
        attributes.class = ${props.cssClass}
        attributes.class.@if.isset = ${!String.isBlank(props.cssClass)}

        // assign css modfieres
        attributes.class.@process.cssModifier = Webandco.Fusion:Component.ClassModifier {
            cssClass = ${props.cssClass}
            cssModifier = ${props.cssModifier}
        }

        // assign href
        attributes.href = Webandco.Fusion:Component.Tag.A.Href {
            linkHref = ${props.linkHref}
            linkEnabled = ${props.linkEnabled}
        }

        // assign target if set
        attributes.target = ${props.linkTarget}
        attributes.target.@if.isset = ${props.linkTarget != false}

        // assign content
        content = ${props.linkText}
    }
}

prototype(Webandco.Fusion:Component.Tag.A.Href) < prototype(Neos.Fusion:Component) {

    linkHref = 'must-be-set'
    linkEnabled = true

    renderer = Neos.Fusion:Case {
        enabled {
            condition = ${Type.isObject(props.linkHref) && props.linkEnabled}
            renderer = ${props.linkHref}
            renderer.@process.convertUris = Neos.Neos:ConvertUris {
                forceConversion = true
            }
        }

        disabled {
            condition = ${true}
            renderer = '#'
        }
    }
}

