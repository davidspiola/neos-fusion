prototype(Webandco.Fusion:Component.Tag.Img) < prototype(Webandco.Fusion:Component) {

    bemClass = 'image'
    bemModifier = Webandco.Fusion:BemModifier

    imageSrc = 'must-be-set'
    imageWidth = null
    imageHeight = null
    imageAlt = ''
    imageTitle = ''

    @styleguide {
        title = 'Tag: img'
        props {
            imageAlt = 'image alt text'
            imageTitle = 'image title text'
        }
    }

    renderer = Webandco.Fusion:Component.Tag {
        tagName = 'img'

        attributes.src = Webandco.Fusion:Component.Tag.Img.Src {
            imageSrc = ${props.imageSrc}
            imageWidth = ${props.imageWidth}
            imageHeight = ${props.imageHeight}
        }

        attributes.alt = ${props.imageAlt}
        attributes.title = ${props.imageTitle}
    }
}

prototype(Webandco.Fusion:Component.Tag.Img.Src) < prototype(Neos.Fusion:Component) {

    imageSrc = 'must-be-set'
    imageWidth = null
    imageHeight = null

    renderer = Neos.Fusion:Case {
        asset {
            condition = ${Type.isObject(props.imageSrc)}
            renderer = Neos.Neos:ImageUri {
                allowCropping = true
                allowUpScaling = true
                asset = ${props.imageSrc}
                width = ${props.imageWidth}
                height = ${props.imageHeight}
            }
        }

        resource {
            condition = ${String.indexOf(props.imageSrc, 'resource') >= 0}
            renderer = Neos.Fusion:ResourceUri {
                path = ${props.imageSrc}
            }
        }

        link {
            condition = ${String.indexOf(props.imageSrc, 'http') >= 0}
            renderer = ${props.imageSrc}
        }

        fallback {
            condition = ${true}
            renderer = '/_Resources/Static/Packages/Neos.Neos/Images/dummy-image.svg'
        }
    }
}

