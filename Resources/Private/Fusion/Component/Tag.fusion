prototype(Webandco.Fusion:Component.Tag) < prototype(Neos.Fusion:Tag) {
    tagName = ${props.tagName}

    // assign tag id
    attributes.id = ${props.tagId}
    attributes.id.@if.isset = ${props.tagId != false && !String.isBlank(String.trim(props.tagId))}

    // assign bem class and css modfieres
    attributes.class = ${String.trim(props.bemClass)}
    attributes.class.@process.cssModifier = Webandco.Fusion:Component.BemModifier {
        bemClass = ${props.bemClass}
        bemModifier = ${props.bemModifier}
    }

    // assign additional css classes
    attributes.class.@process.cssClass = ${String.isBlank(String.trim(props.cssClass)) ? value : String.trim(value + ' ' + props.cssClass)}

    // assign style
    attributes.style = ${props.cssStyle}
    attributes.style.@if.isset = ${props.cssStyle != false && !String.isBlank(String.trim(props.cssStyle))}

    // assign component content
    content = ${props.content}
}

prototype(Webandco.Fusion:Component.Tag.Content) < prototype(Neos.Fusion:Array)
